include "std.goof"
include "string.goof"

const PUTD_BUFFER_CAP 32 end
const MEM_CAPACITY    640000 end

const OP_PUSH_INT 0 end
const OP_PLUS     1 end
const OP_MINUS    2 end
const OP_DUMP     3 end

memory putd-buffer PUTD_BUFFER_CAP end
memory fd          8 end
memory outputfd    8 end
memory statbuf     sizeof(stat) end
memory content     sizeof(str) end
memory line        sizeof(str) end
memory word        sizeof(str) end
memory streq_a     sizeof(str) end
memory streq_b     sizeof(str) end
memory parse_int_a sizeof(str) end
memory ops-count   8 end
memory ops         0 end

const sizeof(Op) 16 end

// 10 -> "10"
proc fputd int -- --
    if dup 0 = do
        "0" outputfd fputs
    else
        putd-buffer PUTD_BUFFER_CAP +
        // n ptr
        while over 0 > do
            1 - dup rot rot
            // ptr ptr n
            10 divmod
            // ptr ptr n%10 n/10
            rot rot swap
            // ptr n/10 ptr n%10
            48 + . swap
        end
        dup // n ptr len
        putd-buffer PUTD_BUFFER_CAP + swap - swap outputfd fputs
   end
   drop
end

proc parse_int ptr int -- int --
    parse_int_a store_str
    0 0 while dup parse_int_a str.count ,64 < do
        dup parse_int_a str.data ,64 + , '0' -
        rot rot 10 * +
        swap
        1 +
    end
    drop
end

proc streq ptr int ptr int -- bool --
    streq_a store_str
    streq_b store_str
    if streq_a str.count ,64 streq_b str.count ,64 = do
        0 while
            if dup streq_a str.count ,64 < do
                dup streq_a str.data ,64 + ,
                over streq_b str.data ,64 + ,
                =
            else
                0 (bool)
            end
        do
            1 +
        end
        dup streq_a str.count ,64 > over streq_a str.count ,64 = | swap drop
    else
        0 (bool)
    end
end

// type operand
proc push_op int int -- --
    ops-count ,64 sizeof(Op) * ops +
    dup 8 + rot rot .64 swap .64
    ops-count inc64
end

proc compile_ops -- --

    O_RDWR O_CREAT O_TRUNC | |
    "./output.asm\x00" swap drop
    AT_FDCWD
    openat

    if dup 0 < do
        "error: could not open file " eputs "./output.asm\n" eputs
        1 exit
    end
    dup MODE0666
    swap
    fchmod drop

    outputfd swap .64

    "BITS 64\n" outputfd fputs
    "segment .text\n" outputfd fputs
    "dump:\n" outputfd fputs
    "    push    rbp\n" outputfd fputs
    "    mov     rbp, rsp\n" outputfd fputs
    "    sub     rsp, 64\n" outputfd fputs
    "    mov     QWORD [rbp-56], rdi\n" outputfd fputs
    "    mov     QWORD [rbp-8], 1\n" outputfd fputs
    "    mov     eax, 32\n" outputfd fputs
    "    sub     rax, QWORD [rbp-8]\n" outputfd fputs
    "    mov     BYTE [rbp-48+rax], 10\n" outputfd fputs
    ".L2:\n" outputfd fputs
    "    mov     rcx, QWORD [rbp-56]\n" outputfd fputs
    "    mov     rdx, -3689348814741910323\n" outputfd fputs
    "    mov     rax, rcx\n" outputfd fputs
    "    mul     rdx\n" outputfd fputs
    "    shr     rdx, 3\n" outputfd fputs
    "    mov     rax, rdx\n" outputfd fputs
    "    sal     rax, 2\n" outputfd fputs
    "    add     rax, rdx\n" outputfd fputs
    "    add     rax, rax\n" outputfd fputs
    "    sub     rcx, rax\n" outputfd fputs
    "    mov     rdx, rcx\n" outputfd fputs
    "    mov     eax, edx\n" outputfd fputs
    "    lea     edx, [rax+48]\n" outputfd fputs
    "    mov     eax, 31\n" outputfd fputs
    "    sub     rax, QWORD [rbp-8]\n" outputfd fputs
    "    mov     BYTE [rbp-48+rax], dl\n" outputfd fputs
    "    add     QWORD [rbp-8], 1\n" outputfd fputs
    "    mov     rax, QWORD [rbp-56]\n" outputfd fputs
    "    mov     rdx, -3689348814741910323\n" outputfd fputs
    "    mul     rdx\n" outputfd fputs
    "    mov     rax, rdx\n" outputfd fputs
    "    shr     rax, 3\n" outputfd fputs
    "    mov     QWORD [rbp-56], rax\n" outputfd fputs
    "    cmp     QWORD [rbp-56], 0\n" outputfd fputs
    "    jne     .L2\n" outputfd fputs
    "    mov     eax, 32\n" outputfd fputs
    "    sub     rax, QWORD [rbp-8]\n" outputfd fputs
    "    lea     rdx, [rbp-48]\n" outputfd fputs
    "    lea     rcx, [rdx+rax]\n" outputfd fputs
    "    mov     rax, QWORD [rbp-8]\n" outputfd fputs
    "    mov     rdx, rax\n" outputfd fputs
    "    mov     rsi, rcx\n" outputfd fputs
    "    mov     edi, 1\n" outputfd fputs
    "    mov     rax, 1\n" outputfd fputs
    "    syscall\n" outputfd fputs
    "    nop\n" outputfd fputs
    "    leave\n" outputfd fputs
    "    ret\n" outputfd fputs
    "global _start\n" outputfd fputs
    "_start:\n" outputfd fputs
    0 while dup ops-count ,64 < do
        dup sizeof(Op) * ops +
        if dup ,64 OP_PUSH_INT = do
            "    ;; -- push int " outputfd fputs dup 8 + ,64 fputd " --\n" outputfd fputs
            "    mov rax, " outputfd fputs dup 8 + ,64 fputd "\n" outputfd fputs
            "    push rax\n" outputfd fputs
        elif dup ,64 OP_PLUS = do
            "    ;; -- plus --\n" outputfd fputs
            "    pop rax\n" outputfd fputs
            "    pop rbx\n" outputfd fputs
            "    add rax, rbx\n" outputfd fputs
            "    push rax\n" outputfd fputs
        elif dup ,64 OP_MINUS = do
            "    ;; -- minus --\n" outputfd fputs
            "    pop rax\n" outputfd fputs
            "    pop rbx\n" outputfd fputs
            "    sub rbx, rax\n" outputfd fputs
            "    push rbx\n" outputfd fputs
        elif dup ,64 OP_DUMP = do
            "    ;; -- dump --\n" outputfd fputs
            "    pop rdi\n" outputfd fputs
            "    call dump\n" outputfd fputs
        else 
            here eputs , write drop ": unreachable\n" eputs 1 exit
        end
        drop
        1 +
    end
    drop
    "    mov rax, 60\n" outputfd fputs
    "    mov rdi, 0\n" outputfd fputs
    "    syscall\n" outputfd fputs
    "segment .data\n" outputfd fputs
    "segment .bss\n" outputfd fputs
    "mem: resb " outputfd fputs MEM_CAPACITY fputd "\n" outputfd fputs
end

proc dump_ops -- --
    0 while dup ops-count ,64 < do
        dup sizeof(Op) * ops +
        "type:    " puts dup ,64 dump
        "operand: " puts 8 + ,64 dump
        "-----------\n" puts
        1 +
    end
    drop
end

if argc 2 < do
    "usage: " eputs 0 nth_argv dup strlen swap eputs " <file>\n" eputs
    "error: no input file provided\n" eputs
    1 exit
end

O_RDONLY
1 nth_argv
AT_FDCWD
openat

if dup 0 < do
    "error: could not open file " eputs 1 nth_argv dup strlen swap eputs "\n" eputs
    1 exit
end

fd swap .64

if statbuf fd ,64 fstat 0 < do
    "error: could not determine size of file " eputs 1 nth_argv dup strlen swap eputs "\n" eputs
    1 exit
end

content str.count statbuf stat.st_size ,64 .64

// void *mmap(void *addr, size_t len, int prot, int flags, int fildes, off_t off);
0                        // offset
fd ,64                   // fd
MAP_PRIVATE              // flags
PROT_READ                // prot
content str.count ,64    // len
0                        // addr

mmap                     // syscall for mmap

content str.data swap .64         // save the returned pointer to content: mem[content] = ptr to file content

if content str.data ,64 0 < do
    "error: could not memory map file " eputs 1 nth_argv dup strlen swap eputs "\n" eputs
    1 exit
end

while content str.count ,64 0 > do
    line content str_chop_line
    while
        line str.count ,64 0 >
    do
        line str_trim_left
        word line str_chop_word
        if word read_str "+" streq do
            OP_PLUS     0  push_op
        elif word read_str "-" streq do
            OP_MINUS    0  push_op
        elif word read_str "dump" streq do
            OP_DUMP     0  push_op
        else
            if word is_number do
                OP_PUSH_INT word read_str parse_int push_op
            else
                1 nth_argv dup strlen swap eputs
                ": " eputs "unknown token: " eputs word read_str eputs "\n" eputs
                1 exit
            end
        end
    end
end


compile_ops

